buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

apply plugin: "kotlin"
apply plugin: 'java'
apply plugin: 'maven'   // enables gradle install
apply plugin: 'org.jetbrains.dokka'

group   = GROUP_NAME
version = VERSION_NAME
archivesBaseName = ARTIFACT_BASE_NAME

description = "AutomationCentral-365"

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility

def junit5Version = '5.4.2'
def mainClass = "com.amcentral365.service.MainKt"

repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "$targetCompatibility"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

jar {   // uber jar
    manifest {
        attributes "Main-Class": mainClass
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


dependencies {
    compile group: 'org.jetbrains.kotlin',  name: 'kotlin-stdlib-jdk8'
    compile group: 'com.google.guava',      name: 'guava',                version: '27.0.1-jre'
    compile group: 'com.sparkjava',         name: 'spark-core',           version: '2.7.2'
    compile group: 'org.mariadb.jdbc',      name: 'mariadb-java-client',  version: '2.4.1'
    compile group: 'com.amcentral365',      name: 'pl4kotlin',            version: '0.3.12'
    compile group: 'io.github.microutils',  name: 'kotlin-logging',       version: '1.5.4'
    compile group: 'org.slf4j',             name: 'slf4j-log4j12',        version: '1.7.26'
    compile group: 'com.google.code.gson',  name: 'gson',                 version: '2.8.5'
    compile group: 'com.github.ajalt',      name: 'clikt',                version: '1.2.0'

    testCompile group: 'org.junit.jupiter',    name: 'junit-jupiter-engine', version: junit5Version
    testCompile group: 'org.junit.jupiter',    name: 'junit-jupiter-api',    version: junit5Version
    testCompile group: 'org.junit.jupiter',    name: 'junit-jupiter-params', version: junit5Version
    testCompile group: 'io.mockk',             name: 'mockk',                version:'1.8.7'
}
