buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: 'java'
apply plugin: 'maven'   // enables gradle install

group = 'com.amcentral'
version = '0.0.1-SNAPSHOT'

description = "AutoMationCentral"

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility

def junit5Version = '5.2.0'

repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
    deploy
    integrationTest {
        kotlin.srcDir    'src/test/kotlin/com/amcentral365/service/integrationTests'
        resources.srcDir 'src/test/resources'
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "$targetCompatibility"
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    configure {
        // Assign all Java system properties from the command line to the JavaExec task:
        //    gradle -DdbConfig=sqlite.properties check
        systemProperties System.properties as Map<String, ?>
    }
}

check.dependsOn integrationTest   // "gradle check" runs integration test


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}


dependencies {
    compile group: 'org.jetbrains.kotlin',  name: 'kotlin-stdlib-jdk8'
    compile group: 'com.google.guava',      name: 'guava',                version: '24.0-jre'
    compile group: 'com.sparkjava',         name: 'spark-core',           version: '2.7.1'
    compile group: 'mysql',                 name: 'mysql-connector-java', version: '5.1.46'
    compile group: 'com.amcentral365',      name: 'pl4kotlin',            version: '0.1.0'
    compile group: 'io.github.microutils',  name: 'kotlin-logging',       version: '1.5.4'
    compile group: 'org.slf4j',             name: 'slf4j-log4j12',        version: '1.7.25'

    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'
    testCompile group: 'org.junit.jupiter',    name: 'junit-jupiter-engine', version: junit5Version
    testCompile group: 'org.junit.jupiter',    name: 'junit-jupiter-api',    version: junit5Version
    testCompile group: 'io.mockk',             name: 'mockk',                version:'1.7.8'
}
